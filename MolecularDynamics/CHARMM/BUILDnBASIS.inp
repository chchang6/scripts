! Input deck to create dihedral basis file for REDU job. Goal to 
! create heavy atom dihedral basis for NM and QH analyses with reduced
! basis.
* Initial no-hydrogen dihedral-only basis creation. CHC 1/20/06
* Unformatted file output.

bomblev -2

! read in topology
open unit 1 read card name ../CHARMM_22PROT+HYDFERR.RTF
read rtf unit 1 card
close unit 1
open unit 1 read card name ../CHARMM_22PROT+HYDFERR.PRM
read param unit 1 card
close unit 1

! Set up ferredoxin segment
open unit 12 name "FERR.pdb" READ FORM
read sequ PDB unit 12
gener FERR setup
read COOR PDB UNIT 12
close unit 12

! Set up [2Fe-2S] cluster segment
open unit 13 name "FRC1.pdb" READ FORM
read sequ PDB unit 13
gener FRC1 FIRST none LAST none NODI setup
READ COOR PDB UNIT 13
close unit 13

! Set up HydA2 segment
open unit 14 name "HYDR.pdb" READ FORM
read sequ PDB unit 14
gener HYDR setup
READ COOR PDB UNIT 14
close unit 14

! Set up H-cluster segment
open unit 15 name "HYDH.pdb" READ FORM
read sequ PDB unit 15
gener HYDH FIRST none LAST none NODI setup
READ COOR PDB UNIT 15
close unit 15

! Set up Dowser segment
open unit 16 name "DOWS.pdb" READ FORM
read sequ PDB unit 16
gener DOWS FIRST none LAST none NODI setup
READ COOR PDB UNIT 16
close unit 16

! Establish cysteine-cluster bonding
patch FE1C HYDH 535 HYDR 208
patch FE2C HYDH 535 HYDR 263
patch FE3C HYDH 535 HYDR 463
patch FE4C HYDH 535 HYDR 467
patch FEX  HYDH 534 HYDR 467
patch FE1C FRC1  97 FERR  39
patch FE1C FRC1  97 FERR  44
patch FE2C FRC1  97 FERR  47
patch FE2C FRC1  97 FERR  77

print coor

! write out PSF and CRD files
open unit 61 name hyd16HSD_dowsed_CHARMM.psf WRIT FORM
write psf CARD unit 61
close unit 61

open unit 62 name hyd16HSD_dowsed.crd WRIT FORM
write coor card unit 62
close unit 62

! Generate internal coordinates
hbonds
IC PARAM
IC BUILD

! Delete dihedrals involving hydrogen
IC DELETE FIRST FOURTH DIHE SELE HYDR END
IC DELETE FIRST FOURTH IMPR SELE HYDR END
PRINT IC

! Generate basis set of dihedral angles only.
VIBRAN ELEC ATOM FSWITCH CDIE -
      VDW VATOM VSWITCHED -
      CUTNB 12 CTOFNB 11 CTONNB 10 WMIN 1.5 EPS 1.0 -
  NMODe 4000
BASIS IC DIHEdral
! Write out normal mode basis
OPEN UNIT 51 NAME NOHYDRDIHE.BAS WRITE UNFORM
WRITE NORM UNIT 51
* Reduced basis generation -- dihedral angles without hydrogen
*
CLOSE UNIT 51
END

stop
