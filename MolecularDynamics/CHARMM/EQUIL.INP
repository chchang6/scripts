! Input deck to equilibrate protein using Langevin dynamics and SHAKE
* Title Initial equilibration of CrferrHSD in vacuum
* CHC, 6/06/05
*

! read in topologies
open unit 11 read card name ../CHARMM_22PROT+HYDFERR.RTF
read rtf card unit 11
close unit 11

! read in parameters
open unit 12 read card name ../CHARMM_22PROT+HYDFERR.PRM
read para card unit 12
close unit 12

! read in psf and coordinate files
open unit 10 read card name "Crferr_HSP.psf"
read psf card unit 10
close unit 10

open read unit 10 card name "vac_min_out.crd"
read coor card unit 10
close unit 10

! Dynamics with cutoffs for non-bonded interactions.
! Initial equilibration using Langevin dynamics

OPEN UNIT 20 NAME restart WRIT FORM
OPEN UNIT 50 NAME initequil.dcd WRIT UNFORM
OPEN UNIT 70 NAME energies WRIT FORM

SCALAR FBETA SET 50

SHAKE  BONH FAST
DYNA   LEAP LANGEVIN STRT NSTEp 20000 -
       IPRFRQ 1000 IHTFRQ 0 IEQFRQ 0 NTRFRQ 0 -
       IUNWri 20 IUNCRD 50 IUNLEV -1 KUNIT 70 -
       NPRINT 100 NSAVC 100 NSAVV 0 IHBFRQ 0 INBFRQ 25 -
       ILBFRQ 1000 RBUFFER 0.0 -
       ELEC ATOM FSWITCH CDIE -
       VDW VATOM VSWITCHED -
       CUTNB 12 CTOFNB 11 CTONNB 10 WMIN 1.5 EPS 1.0 -
       FIRSTT 300. FINALT 300. TBATH 300.0 -
       IASORS 0 IASVEL 1 ISCVEL 0 ICHECW 0 TWINDH 0.0 TWINDL 0.0
OPEN UNIT 10 NAME initequil.crd WRIT CARD
WRITe COOR UNIT 10 CARD
* Coordinates after initial equilibration
*
CLOSE UNIT 70

stop
