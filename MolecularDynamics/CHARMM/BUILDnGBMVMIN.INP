! Input deck to build structure files from PDB input.
* Title Build of CHARMM coordinate file from all-atom PDB file
*

bomblev -2

! read in topology
open unit 10 name "./CHARMM_22PROT+HYDFERR.RTF" READ FORM
read rtf unit 10 card
close unit 10

! Read in parameters
open unit 11 name "./CHARMM_22PROT+HYDFERR.PRM" READ FORM
read param unit 11 card
close unit 11

! Set up ferredoxin segment
open unit 12 name "FERR.pdb" READ FORM
read sequ PDB unit 12
gener FERR setup
read COOR PDB UNIT 12
close unit 12

! Set up [2Fe-2S] cluster segment
open unit 13 name "FRC1.pdb" READ FORM
read sequ PDB unit 13
gener FRC1 FIRST none LAST none NODI setup
READ COOR PDB UNIT 13
close unit 13

! Establish cysteine-cluster bonding
patch FE1C FRC1 97 FERR  39
patch FE1C FRC1 97 FERR  44
patch FE2C FRC1 97 FERR  47
patch FE2C FRC1 97 FERR  77

print coor

! write out PSF and CRD files
open unit 61 name crferrhsd.psf WRIT FORM
write psf CARD unit 61
close unit 61

open unit 62 name crferrhsd.crd WRIT FORM
write coor card unit 62

! Set up GBMV minimization
! GBMV Setup
GBMV GEOM BETA -20 EPSILON 78.5 DN 1.0 WATR 1.4 LAMBDA1 0.5 TOL 1.0E8 -
   BUFR 0.5 MEM 10 CUTA 20 HSX1 -0.125 HSX2 0.25 ONX 1.9 OFFX 2.1 -
   ALFRQ 1 EMP 0.25 P1 0.45 P2 1.25 P3 0.7 P6 8.0 -
   SA 0.0072 SB 0.0 KAPPA 0.103 WTYP 2 NPHI 38 CORR 1 SHIFT -0.102 -
   SLOPE 0.9085

! Minimization
MINI ABNR NSTEP 3000 TOLGRD 0.000002 FMEM=0.1 INBFRQ 10 -
     ELEC ATOM FSWITCH CDIE -
     VDW VATOM VSWITCHED -
     CUTNB 16 CTOFNB 14 CTONNB 12 WMIN 1.5

! Open file to write coordinates
OPEN UNIT 51 NAME gbmv_min_out.crd WRIT FORM
WRIT COOR CARD UNIT 51

OPEN UNIT 52 NAME gbmv_min_out.pdb WRIT FORM
WRIT COOR PDB UNIT 52
CLOSE UNIT 52

! Clear GB arrays
GBMV Clear

stop
