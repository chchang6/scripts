! Input deck to calculate Generalized Born solvation energy of protein
! with parameters from Noskov & Lim, Biophys J. 81: 737-750 (2001).
! Annealed NAMD structure as input, minimize further 1000 steps with GBMV.
* Title Generalized Born calculation of solvation energy.
* CrferrHSD CHC 9/8/05
*

! read in topologies
open unit 11 read card name ../CHARMM_22PROT+HYDFERR.RTF
read rtf card unit 11
close unit 11

! read in parameters
open unit 12 read card name ../CHARMM_22PROT+HYDFERR.PRM
read para card unit 12
close unit 12

! read in psf file
open unit 10 read card name crferrhsd_charmm.psf
read psf card unit 10
close unit 10

! read in coordinates
OPEN UNIT 10 READ CARD NAME anneal_out.pdb
READ COOR PDB OFFS -30 SELE ALL END UNIT 10
CLOSE UNIT 10

GBMV GEOM FIXA BETA -20 EPSILON 80 DN 1.0 WATR 1.4 -
   LAMBDA1 0.705 BUFR 0.0 CUTA 30 OFFX 2.5 ALFRQ 1 -
   P1 0.448 P2 0.173 P3 .013 P4 9.015 P5 0.900 P6 8.0 -
   SHIFT -0.102 SLOPE 0.9085 SA 0.0072 SB 0.0 NPHI 38 CORR 1

!ENER ELEC ATOM FSWITCH CDIE -
!     VDW VATOM VSWITCHED -
!     CUTNB 999 CTOFNB 989 CTONNB 979 WMIN 1.5 EPS 1.0
!
MINI ABNR NSTEP 10000 TOLGRD 1E-09 TOLENR 1E-09 FMEM=0.1 INBFRQ 10 -
     ELEC ATOM FSWITCH CDIE -
     VDW VATOM VSWITCHED -
     CUTNB 13 CTOFNB 12 CTONNB 8 WMIN 1.5 EPS 1.0 -
END

GBMV CLEAR

OPEN UNIT 61 NAME GBMVmin_out.crd WRIT FORM
COOR WRIT CARD UNIT 61
CLOSE UNIT 61

stop


